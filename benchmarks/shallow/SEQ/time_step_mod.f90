module time_step_mod
  use kind_params_mod
  use field_mod
  implicit none

contains

  subroutine invoke_time_step(cufld, cvfld, ufld, unew, uold, &
                              vfld, vnew, vold, &
                              pfld, pnew, pold, &
                              hfld, zfld, tdt)
    use compute_cu_mod, only: compute_cu_code
    use compute_cv_mod, only: compute_cv_code
    use compute_z_mod,  only: compute_z_code
    use compute_h_mod,  only: compute_h_code
    use compute_unew_mod, only: compute_unew_code
    use compute_vnew_mod, only: compute_vnew_code
    use compute_pnew_mod, only: compute_pnew_code
    use time_smooth_mod, only: time_smooth_code
    use apply_bcs_mod, only: invoke_apply_bcs
    implicit none
    type(r2d_field), intent(inout) :: cufld, cvfld
    type(r2d_field), intent(inout) :: unew, vnew, pnew
    type(r2d_field), intent(inout) :: hfld, zfld, pfld, &
                                               ufld, vfld
    type(r2d_field), intent(inout) :: uold, vold, pold
    real(go_wp),     intent(in)    :: tdt

    ! Locals
    integer     :: I, J, idim1, idim2
    real(go_wp) :: dx, dy

    ! COMPUTE CAPITAL U, CAPITAL V, Z AND H

    do J=cufld%internal%ystart, cufld%internal%ystop
       do I=cufld%internal%xstart, cufld%internal%xstop

          call compute_cu_code(i, j, cufld%data, pfld%data, ufld%data)
       end do
    end do
    do J=cvfld%internal%ystart, cvfld%internal%ystop
       do I=cvfld%internal%xstart, cvfld%internal%xstop

          call compute_cv_code(i, j, cvfld%data, pfld%data, vfld%data)
       end do
    end do
    dx = zfld%grid%dx
    dy = zfld%grid%dy

    do J=zfld%internal%ystart, zfld%internal%ystop, 1
       do I=zfld%internal%xstart, zfld%internal%xstop, 1

          call compute_z_code(i, j,      &
                              zfld%data, &
                              pfld%data, &
                              ufld%data, &
                              vfld%data, &
                              dx, dy)

       end do
    end do

    DO J=hfld%internal%ystart, hfld%internal%ystop, 1
       DO I=hfld%internal%xstart, hfld%internal%xstop, 1

          CALL compute_h_code(i, j, hfld%data, &
                              pfld%data, ufld%data, vfld%data)
       END DO
    END DO

    ! PERIODIC CONTINUATION

    ! This call could be generated automatically by PSyclone
    call invoke_apply_bcs(CUfld)
    call invoke_apply_bcs(CVfld)
    call invoke_apply_bcs(Hfld)
    call invoke_apply_bcs(Zfld)

    ! COMPUTE NEW VALUES U,V AND P
    dx = unew%grid%dx

    DO J=unew%internal%ystart, unew%internal%ystop, 1
       DO I=unew%internal%xstart, unew%internal%xstop, 1

          CALL compute_unew_code(i, j,                  &
                                 unew%data, uold%data,  &
                                 zfld%data, cvfld%data, &
                                 hfld%data, tdt, dx)
       END DO
    END DO

    dy = vnew%grid%dy

    DO J=vnew%internal%ystart, vnew%internal%ystop, 1
       DO I=vnew%internal%xstart, vnew%internal%xstop, 1

          CALL compute_vnew_code(i, j,                  &
                                 vnew%data, vold%data,  &
                                 zfld%data, cufld%data, &
                                 hfld%data, tdt, dy)
       END DO
    END DO
    dx = pnew%grid%dx
    dy = pnew%grid%dy

    DO J=pnew%internal%ystart, pnew%internal%ystop, 1
       DO I=pnew%internal%xstart, pnew%internal%xstop, 1

          CALL compute_pnew_code(i, j,                 &
                                 pnew%data, pold%data, &
                                 cufld%data, cvfld%data, &
                                 tdt, dx, dy)
       END DO
    END DO

    ! PERIODIC CONTINUATION

    ! This call could be generated by PSyclone
    call invoke_apply_bcs(UNEW)
    call invoke_apply_bcs(VNEW)
    call invoke_apply_bcs(PNEW)

    ! Time is in seconds but we never actually need it
    !CALL increment(time, dt)

    ! TIME SMOOTHING AND UPDATE FOR NEXT CYCLE
    
    ! Here we field objects to get at raw data.
    idim1 = SIZE(ufld%data, 1)
    idim2 = SIZE(ufld%data, 2)

    ! Loop over 'columns'
    DO J=1,idim2
      DO I=1,idim1
         CALL time_smooth_code(i, j, &
                               ufld%data, unew%data, uold%data)
      END DO
    END DO

    ! Here we will query what should be field objects to get at
    ! raw data.
    idim1 = SIZE(vfld%data, 1)
    idim2 = SIZE(vfld%data, 2)

    ! Loop over 'columns'
    DO J=1,idim2
      DO I=1,idim1
         CALL time_smooth_code(i, j, &
                               vfld%data, vnew%data, vold%data)
      END DO
    END DO

    ! Here we will query what should be field objects to get at
    ! raw data.
    idim1 = SIZE(pfld%data, 1)
    idim2 = SIZE(pfld%data, 2)

    DO J=1,idim2
      DO I=1,idim1
         CALL time_smooth_code(i, j, &
                               pfld%data, pnew%data, pold%data)
      END DO
    END DO

    call copy_field(UNEW, Ufld)
    call copy_field(VNEW, Vfld)
    call copy_field(PNEW, pfld)

  end subroutine invoke_time_step

end module time_step_mod
